#!/usr/bin/perl -w
# -*- perl -*-

#
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;

Getopt::Long::Configure('pass_through');

my $include_orig;
my $diff_file;
GetOptions("include-orig!" => \$include_orig,
	   "diff-file=s"   => \$diff_file,
	   "help|?" => sub { show_usage() },
	  );

my @cmd;
if ($diff_file) {
    @cmd = ('cat', $diff_file);
    @ARGV = ();
} elsif (-d "RCS") {
    @cmd = qw(rcsdiff);
    die "RCS support is NYI";
} else {
    @cmd = qw(cvs diff);
}
push @cmd, @ARGV;

use constant STAGE_SHOW => 1;
use constant STAGE_HIDE => 2;
my $stage = STAGE_SHOW;
my $current_file;

my $bbdrx = qr{^> ([^\t]+)\t(\S+)\s([ 0-9,.+-]+)$};

for my $line (`@cmd`) {
    chomp $line;
    my $new_file_started = 0;
    if ($line =~ /^Index:\s+(.*)/) {
	$current_file = $1;
	$new_file_started = 1;
    }

    if ($new_file_started) {
	if ($current_file =~ m{(bbbike-temp-blockings-optimized\.pl
			       |sehenswuerdigkeit_img/
			       |\.coords\.data
			       |Makefile
			       |\.modified
			       |BASE
			       )}x) {
	    $stage = STAGE_HIDE;
	} elsif (!$include_orig && $current_file =~ /-orig/) {
	    $stage = STAGE_HIDE;
	} else {
	    $stage = STAGE_SHOW;
	}
    }
    if ($stage == STAGE_SHOW) {
	if ($new_file_started) {
	    print "# File: $current_file\n";
	} elsif ($line =~ m{$bbdrx}o) {
	    my $name   = $1;
	    my $cat    = "#000080";
	    my $coords = $3;
	    print "$name\t$cat $coords\n";
	}
    }
}

sub show_usage {
    print STDERR <<EOF;
Usage examples:
    cd .../bbbike/data
    ../miscsrc/cvsdiffbbd -D2007/05/01 > /tmp/20070501.bbd
    ../miscsrc/cvsdiffbbd -Dyesterday > /tmp/yesterday.bbd

EOF
    exit 0;
}
__END__

for y in 2004 2005 2006 2007; do for m in 01 02 03 04 05 06 07 08 09 10 11 12; do echo $y$m; ~/src/bbbike/miscsrc/cvsdiffbbd -D$y/$m/01 -D$y/$m/`perl -MDate::Calc=Days_in_Month -e "print Days_in_Month(@ARGV)" $y $m` >| /tmp/$y$m.bbd; done;done
